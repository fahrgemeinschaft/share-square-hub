plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'groovy'
}

sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

Properties properties = new Properties()
def propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
}
def nexusUrl = properties.getProperty('nexus.url') != null ? properties.getProperty('nexus.url') : System.getenv("NEXUS_URL")
def nexusUser = properties.getProperty('nexus.user') != null ? properties.getProperty('nexus.user') : System.getenv("NEXUS_USER")
def nexusPass = properties.getProperty('nexus.pass') != null ? properties.getProperty('nexus.pass') : System.getenv("NEXUS_PASS")

repositories {
    mavenCentral()

    maven {
        url = nexusUrl
        credentials {
            username = nexusUser
            password = nexusPass
        }
    }
}

sourceSets {
    main {
        java {
        	srcDirs= ['src/main/java']
        }
        resources {
        	srcDirs= ['src/main/resources']
        }
    }
    test {
        java {
        	srcDirs= ['src/test/java','src/test/groovy']
        }
        resources {
        	srcDirs= ['src/test/resources']
        }
    }
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-ui:1.4.3'
    implementation 'org.springframework.data:spring-data-commons:2.3.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:2.3.1.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	//implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    
    implementation group: 'org.sharesquare.commons', name: 'share-square-commons', version: "0.0.1-SNAPSHOT", changing: true

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
    //testCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.4.1'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.codehaus.groovy:groovy:3.0.4'
	testImplementation 'org.codehaus.groovy:groovy-all:3.0.4'
	testImplementation 'org.spockframework:spock-spring:2.0-M3-groovy-3.0'
	testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
	//testImplementation 'com.athaydes:spock-reports:2.0-RC2'
}

test {
	useJUnitPlatform()
	//systemProperty 'com.athaydes.spockframework.report.showCodeBlocks', true
}
